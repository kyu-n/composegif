plugins {
    id 'application'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation libs.commons.imaging
    implementation libs.flatlaf
    implementation libs.twelvemonkeys.imageio.psd

    testImplementation libs.junit.jupiter
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'com.composegif.ComposeGifApp'
    applicationDefaultJvmArgs = [
        '-Xmx256m',
        '-XX:+UseZGC',
        '-Dsun.java2d.opengl=true'
    ]
}

tasks.named('test') {
    useJUnitPlatform()
}

// --- Distribution: fat JAR + jlink runtime ---

tasks.named('jar') {
    archiveFileName = 'composegif.jar'
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest {
        attributes 'Main-Class': 'com.composegif.ComposeGifApp'
    }
    from {
        configurations.runtimeClasspath.filter { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

def runtimeDir = layout.buildDirectory.dir('runtime')
def jreTarget = providers.environmentVariable('JRE_TARGET')

def createRuntime = tasks.register('createRuntime', Exec) {
    dependsOn 'clean'
    doFirst {
        runtimeDir.get().asFile.deleteDir()
    }
    workingDir = layout.projectDirectory.asFile
    commandLine(
        jreTarget.map { "${it}/bin/jlink" }.get(),
        '--add-modules', 'java.se,java.base,java.desktop,jdk.unsupported,jdk.crypto.ec',
        '--strip-debug',
        '--no-header-files',
        '--no-man-pages',
        '--vm=server',
        '--compress=2',
        '--output', runtimeDir.get().asFile.absolutePath
    )
}

tasks.named('jar') {
    dependsOn createRuntime
}

tasks.register('dist') {
    dependsOn 'jar'
}
